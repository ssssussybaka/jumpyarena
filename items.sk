
on script load:
	clear {Items::*}
	set {Items::Leap} to feather named "&eLeap &8[RIGHT-CLICK]" with lore "&7Boost yourself forwards."
	set {Items::CheckpointTeleport} to light weighted pressure plate named "&eTeleport to checkpoint &8[RIGHT-CLICK]" with lore "&7Teleport you to the checkpoint."
	set {Items::LeaveGame} to clock named "&eLeave to lobby &8[RIGHT-CLICK]" with lore "&7Leave the game you are playing."
	set {Items::JoinGame} to clock named "&eJoin game &8[RIGHT-CLICK]" with lore "&7Join the game!"

on right click:
	if tool of player is {Items::Leap}:
		set {_waited} to difference between {Leap::%uuid of player%::lastUsed} and now
		set {_waitTime} to difference between {_waited} and 30 seconds
		if {_waited} is less than 30 seconds:
			send action bar "&7Please wait %{_waitTime}%" to player
			stop
		push player forward at speed 1.8
		set {Leap::%uuid of player%::lastUsed} to now
	else if tool of player is {Items::LeaveGame}:
		leaveGame(player)
	else if tool of player is {Items::CheckpointTeleport}:
		teleportToLevel(player)
	else if tool of player is {Items::JoinGame}:
		joinGame(player)
